// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    PC(in=A, load=JUMP, inc=true, reset=reset, out=pcint, out[0..14]=pc);

    Not(in=instruction[15], out=Amode);
    Or(a=Amode, b=instruction[5], out=loadA);
    Mux16(a[0..14]=instruction[0..14], a[15]=false, b=calc, sel=instruction[15], out=newA);
    ARegister(in=newA, load=loadA, out=A);
    Or16(a=A, b[0..15]=false, out[0..14]=addressM);

    Mux16(a=D, b=calc, sel=instruction[15], out=newD);
    DRegister(in=newD, load=instruction[4], out=D);

    Mux16(a=D, b=D, sel=instruction[12], out=X);
    Mux16(a=A, b=inM, sel=instruction[12], out=Y);

    ALU(x=X, y=Y, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=calc, out=outM, zr=zr, ng=ng);
    
    And(a=instruction[3], b=instruction[15], out=writeM);

    Not(in=zr, out=nzr);
    Not(in=ng, out=nng);
    And(a=nzr, b=nng, out=jgt);
    And(a=zr, b=true, out=jeq);
    And(a=nng, b=true, out=jge);
    And(a=nzr, b=ng, out=jlt);
    And(a=nzr, b=true, out=jne);
    Or(a=zr, b=ng, out=jle);
    Not(in=false, out=jmp);
    Mux(a=false, b=jgt, sel=instruction[0], out=j0);
    Mux(a=jeq, b=jge, sel=instruction[0], out=j1);
    Mux(a=jlt, b=jne, sel=instruction[0], out=j2);
    Mux(a=jle, b=jmp, sel=instruction[0], out=j3);
    Mux(a=j0, b=j1, sel=instruction[1], out=j4);
    Mux(a=j2, b=j3, sel=instruction[1], out=j5);
    Mux(a=j4, b=j5, sel=instruction[2], out=j6);
    And(a=instruction[15], b=j6, out=JUMP);

    //Mux16(a=pcint, b=A, sel=JUMP, out=pcjump);


}